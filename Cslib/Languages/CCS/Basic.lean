/-
Copyright (c) 2025 Fabrizio Montesi. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fabrizio Montesi
-/

/-! # Calculus of Communicating Systems (CCS)

CCS [Milner80], as presented in [Sangiorgi2011]. In the semantics (see `CCS.lts`), we adopt the
option of constant definitions (K = P).

## Main definitions
- `CCS.Process`: processes.
- `CCS.Context`: contexts.

## Main results
- `CCS.Context.complete`: any process is equal to some context filled an atomic process
(nil or a constant).

## References

* [R. Milner, *A Calculus of Communicating Systems*][Milner80]
* [D. Sangiorgi, *Introduction to Bisimulation and Coinduction*][Sangiorgi2011]
-/

variable (Name : Type u) (Constant : Type v)

namespace CCS

/-- Actions. -/
inductive Act : Type u where
  | name (a : Name)
  | coname (a : Name)
  | τ
deriving DecidableEq

/-- Processes. -/
inductive Process : Type (max u v) where
  | nil
  | pre (μ : Act Name) (p : Process)
  | par (p q : Process)
  | choice (p q : Process)
  | res (a : Name) (p : Process)
  | const (c : Constant)
deriving DecidableEq

@[grind]
def Act.IsVisible (μ : Act Name) : Prop :=
  match μ with
  | name _ => True
  | coname _ => True
  | τ => False

/-- The type of visible actions. -/
abbrev VisibleAct := { μ : Act Name // μ.IsVisible }

/-- Co action. -/
@[grind]
def VisibleAct.co (μ : VisibleAct Name) : VisibleAct Name :=
  match h : μ.val with
  | Act.name a => ⟨Act.coname a, by grind⟩
  | Act.coname a => ⟨Act.name a, by grind⟩
  | .τ => by grind

/-- `Act.co` is an involution. -/
theorem Act.co.involution (μ : VisibleAct Name) : μ.co.co = μ := by grind

@[grind]
def Act.IsDual {Name : Type u} [DecidableEq Name] : Act Name → Act Name → Prop
  | name a, coname b => a = b
  | coname a, name b => a = b
  | _, _ => False

@[grind]
theorem Act.IsDual.comm [DecidableEq Name] (μ μ' : Act Name) (h : Act.IsDual μ μ') :
    μ'.IsDual μ := by
  unfold Act.IsDual at *
  grind

/-- Contexts. -/
inductive Context : Type (max u v) where
  | hole
  | pre (μ : Act Name) (c : Context)
  | parL (c : Context) (q : Process Name Constant)
  | parR (p : Process Name Constant) (c : Context)
  | choiceL (c : Context) (q : Process Name Constant)
  | choiceR (p : Process Name Constant) (c : Context)
  | res (a : Name) (c : Context)
deriving DecidableEq

/-- Replaces the hole in a `Context` with a `Process`. -/
def Context.fill {Name : Type u} {Constant : Type v} (c : Context Name Constant) (p : Process Name Constant) : Process Name Constant :=
  match c with
  | hole => p
  | pre μ c => Process.pre μ (c.fill p)
  | parL c r => Process.par (c.fill p) r
  | parR r c => Process.par r (c.fill p)
  | choiceL c r => Process.choice (c.fill p) r
  | choiceR r c => Process.choice r (c.fill p)
  | res a c => Process.res a (c.fill p)

/-- Any `Process` can be obtained by filling a `Context` with an atom. This proves that `Context`
is a complete formalisation of syntactic contexts for CCS. -/
theorem Context.complete (p : Process Name Constant) : ∃ c : Context Name Constant, p = (c.fill Process.nil) ∨ ∃ k : Constant, p = c.fill (Process.const k) := by
  induction p
  case nil =>
    exists hole
    left
    simp [fill]
  case pre μ p ih =>
    obtain ⟨c, hc⟩ := ih
    exists pre μ c
    simp [fill]
    assumption
  case par p q ihp ihq =>
    obtain ⟨cp, hcp⟩ := ihp
    exists parL cp q
    simp [fill]
    assumption
  case choice p q ihp ihq =>
    obtain ⟨cp, hcp⟩ := ihp
    exists choiceL cp q
    simp [fill]
    assumption
  case res a p ih =>
    obtain ⟨c, hc⟩ := ih
    exists res a c
    simp [fill]
    assumption
  case const k =>
    exists hole
    right
    exists k

end CCS
