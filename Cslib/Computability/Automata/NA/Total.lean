/-
Copyright (c) 2025 Ching-Tsun Chou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ching-Tsun Chou
-/

module

public import Cslib.Computability.Automata.NA.Basic
public import Cslib.Foundations.Semantics.LTS.Total

/-! # Making a nondeterministic automaton total.
-/

@[expose] public section

namespace Cslib.Automata.NA

open Option ωSequence Acceptor

variable {Symbol State : Type*}

/-- `NA.totalize` makes the original NA total by replacing its LTS with `LTS.totalize`
and its starting states with their lifted non-sink versions. -/
def totalize (na : NA State Symbol) : NA (Option State) Symbol where
  toLTS := na.toLTS.totalize
  start := some '' na.start

variable {na : NA State Symbol}

/-- In an infinite execution of `NA.totalize`, as long as the NA stays in a non-sink state,
the execution so far corresponds to a finite execution of the original NA. -/
theorem totalize_run_mtr {xs : ωSequence Symbol} {ss : ωSequence (Option State)} {n : ℕ}
    (h : na.totalize.Run xs ss) (hl : (ss n).isSome) :
    ∃ s t, na.MTr s (xs.take n) t ∧ s ∈ na.start ∧ ss 0 = some s ∧ ss n = some t := by
  obtain ⟨s, _, eq₁⟩ := h.start
  obtain ⟨t, eq₂⟩ := isSome_iff_exists.mp hl
  use s, t
  refine ⟨?_, by grind⟩
  -- TODO: `grind` does not use congruence relations with `na.totalize.MTr`
  rw [← LTS.totalize.nonsink_mtr_iff, ← extract_eq_take, eq₁, ← eq₂]
  exact LTS.OmegaExecution.extract_mTr h.trans (by grind)

/-- Any finite execution of the original NA can be extended to an infinite execution of
`NA.totalize`, provided that the alphabet is inbabited. -/
theorem totalize_mtr_run [Inhabited Symbol] {xl : List Symbol} {s t : State}
    (hs : s ∈ na.start) (hm : na.MTr s xl t) :
    ∃ xs ss, na.totalize.Run (xl ++ω xs) ss ∧ ss 0 = some s ∧ ss xl.length = some t := by
  grind [totalize, Run, LTS.Total.extend_omegaExecution <| LTS.totalize.nonsink_mtr_iff.mpr hm]

namespace FinAcc

/-- `NA.totalize` and the original NA accept the same language of finite words,
as long as the accepting states are also lifted in the obvious way. -/
theorem totalize_language_eq {na : FinAcc State Symbol} :
    language (FinAcc.mk na.totalize (some '' na.accept)) = language na := by
  ext xl
  simp +instances [totalize]

end FinAcc

end Cslib.Automata.NA
